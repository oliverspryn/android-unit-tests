plugins {
    id 'com.android.application'
    id 'de.mannodermaus.android-junit5'
    id 'jacoco'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.1'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId 'com.oliverspryn.medium.androidunittests'

        minSdkVersion 21
        targetSdkVersion 29

        versionCode 1
        versionName '1.0.0'
    }

    buildTypes {
        debug {}
        release { minifyEnabled false }
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }

            jacocoOptions {
                csv.enabled = false
                html.enabled = true
                xml.enabled = false
            }
        }

        unitTests.all {
            testLogging.events = ['failed', 'passed', 'skipped']
        }
    }
}

dependencies {

    // region Application

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // endregion

    // region Unit Tests

    // Expekt
    testImplementation 'com.winterbe:expekt:0.5.0'

    // Kotlin
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Mockito
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.5.0'

    // JUnit
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.junit.platform:junit-platform-runner:1.3.2'

    // Spek
    testImplementation 'org.spekframework.spek2:spek-dsl-jvm:2.0.8'
    testImplementation 'org.spekframework.spek2:spek-runner-junit5:2.0.8'

    // endregion
}
